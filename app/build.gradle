/*
 * @Copyright 2020 HardBackNutter
 * @License GNU General Public License
 *
 * This file is part of NeverTooManyBooks.
 *
 * In August 2018, this project was forked from:
 * Book Catalogue 5.2.2 @2016 Philip Warner & Evan Leybourn
 *
 * Without their original creation, this project would not exist in its
 * current form. It was however largely rewritten/refactored and any
 * comments on this fork should be directed at HardBackNutter and not
 * at the original creators.
 *
 * NeverTooManyBooks is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * NeverTooManyBooks is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with NeverTooManyBooks. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

/*
    $HOME/.gradle/gradle.properties:
    NeverTooManyBooks.signing=$HOME/blah/blah/NeverTooManyBooks.signing

    $HOME/blah/blah/NeverTooManyBooks.signing:
    android {
        signingConfigs {
            debug {
                storeFile file('/path/to/keystore.jks')
                storePassword 'myPassword'
                keyAlias = 'myDebugKey'
                keyPassword 'myPassword'
            }
            beta {
                storeFile file('/path/to/keystore.jks')
                storePassword 'myPassword'
                keyAlias = 'myBetaKey'
                keyPassword 'myPassword'
            }
            release {
                storeFile file('/path/to/keystore.jks')
                storePassword 'myPassword'
                keyAlias = 'myReleaseKey'
                keyPassword 'myPassword'
            }
        }

        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
            beta {
                signingConfig signingConfigs.beta
            }
            release {
                signingConfig signingConfigs.release
            }
        }
    }
 */
if (project.hasProperty("NeverTooManyBooks.signing")
        && new File(project.property("NeverTooManyBooks.signing")).exists()) {
    apply from: project.property("NeverTooManyBooks.signing")
}

/*
    $HOME/.gradle/gradle.properties:
    NeverTooManyBooks.apiKeys=$HOME/blah/blah/NeverTooManyBooks.apiKeys

    $HOME/blah/blah/NeverTooManyBooks.apiKeys:
    android {
        defaultConfig {
            buildConfigField('String', 'GOODREADS_PUBLIC_KEY', '"realKey"')
            buildConfigField('String', 'GOODREADS_PRIVATE_KEY', '"realSecret"')
        }
    }
 */
if (project.hasProperty("NeverTooManyBooks.apiKeys")
        && new File(project.property("NeverTooManyBooks.apiKeys")).exists()) {
    apply from: project.property("NeverTooManyBooks.apiKeys")
}

android {
    compileSdkVersion 29

    // another site: https://gs.statcounter.com/
    // https://gs.statcounter.com/os-version-market-share/android/mobile-tablet/worldwide
    // 2020-04-01 world wide:
    // 10.0     12.45
    // 9.0      51.02
    // 8.1      62.84
    // 8.0      70.46
    // 7.0      76.89
    // 6.0      86.26

    //  2020-04-11.: from Android Studio 'new project' page
    //  A       API     %
    // 10       29       8.2
    //  9       28      39.5
    //  8.1     27      53.5
    //  8       26      60.8
    //  7.1     25      66.2
    //  7       24      73.7
    //  6       23      84.9â€¬
    //
    // The scanner library for pre-24 API is no longer updated.
    //
    defaultConfig {
        applicationId 'com.hardbacknutter.nevertoomanybooks'
        minSdkVersion 23
        targetSdkVersion 29
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // API keys. These are dummy placeholders. Their values should be empty, i.e. '""'.
        // The real ones are in NeverTooManyBooks.apiKeys; see above
        if (getBuildConfigFields().get('GOODREADS_PUBLIC_KEY') == null) {
            buildConfigField('String', 'GOODREADS_PUBLIC_KEY', '""')
            buildConfigField('String', 'GOODREADS_PRIVATE_KEY', '""')
        }

        // If set, we'll use {@link MenuPickerDialogFragment} where implemented.
        buildConfigField('boolean', 'MENU_PICKER_USES_FRAGMENT', 'false')

        // 2020-03-27. Started getting "APIs Temporarily disabled" on LIBRARY_THING
        // searches and "Temporarily down on images.
        // Last checked: 2020-05-21
        buildConfigField('boolean', 'ENABLE_LIBRARY_THING', 'false')
        // but the alternative edition link still works (for now?).
        buildConfigField('boolean', 'ENABLE_LIBRARY_THING_ALT_ED', 'true')

        buildConfigField('boolean', 'ENABLE_GOOGLE_BOOKS', 'true')

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        beta {
            initWith release
            versionNameSuffix "-beta_" + new Date().format('yyyy-MM-dd')
        }
        debug {
            versionNameSuffix "_" + new Date().format('yyyy-MM-dd_HH:mm')

            buildConfigField('boolean', 'MENU_PICKER_USES_FRAGMENT', 'true')
        }

    }

    lintOptions {
        //disable 'MissingTranslation'

        // allow command line builds to complete
        abortOnError false
    }

    compileOptions {
        // https://developer.android.com/studio/releases/#4-0-0-desugar
        coreLibraryDesugaringEnabled true

        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        unitTests.includeAndroidResources = true
        // Can be enabled to make Log.x work. OR, use Logger.x
        unitTests.returnDefaultValues = false
        unitTests.all {
            reports {
                junitXml.enabled = true
                html.enabled = false
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/README.md'
        exclude 'META-INF/CHANGES'
    }
}

// AndroidX release notes:  https://developer.android.com/jetpack/androidx/versions
dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'
    //
    implementation 'androidx.activity:activity:1.2.0-alpha05'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.core:core:1.4.0-alpha01'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.0-rc01'
    implementation 'androidx.exifinterface:exifinterface:1.3.0-alpha01'
    implementation 'androidx.fragment:fragment:1.3.0-alpha05'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha03'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'

    // https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.2.0-beta01'

    // barcode decoder using https://developers.google.com/android/guides/setup
    implementation 'com.google.android.gms:play-services-vision:20.0.0'

    implementation 'ch.acra:acra-mail:5.5.1'
    implementation 'ch.acra:acra-toast:5.5.1'
    implementation 'ch.acra:acra-dialog:5.5.1'
    // OAuth for Goodreads secure access. https://github.com/mttkay/signpost
    implementation 'oauth.signpost:signpost-core:1.2.1.2'
    // Tar file support for backups
    // http://commons.apache.org/proper/commons-compress/
    implementation 'org.apache.commons:commons-compress:1.20'
    // http://commons.apache.org/proper/commons-codec/
    implementation 'commons-codec:commons-codec:1.13'

    // jsoup HTML parser library @ https://jsoup.org/
    implementation 'org.jsoup:jsoup:1.13.1'

    // Embedded ZXing: https://github.com/journeyapps/zxing-android-embedded
    // supporting API <24
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'
    // supporting API 24+
    //implementation 'com.journeyapps:zxing-android-embedded:4.0.0'

    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.json:json:20200518'

    // if enabling, uncomment the App.onCreate as well
//    implementation 'com.facebook.stetho:stetho:1.5.1'
}
