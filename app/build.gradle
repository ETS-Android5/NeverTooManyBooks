/*
 * @Copyright 2018-2021 HardBackNutter
 * @License GNU General Public License
 *
 * This file is part of NeverTooManyBooks.
 *
 * NeverTooManyBooks is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * NeverTooManyBooks is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with NeverTooManyBooks. If not, see <http://www.gnu.org/licenses/>.
 */

import java.time.LocalDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

apply plugin: 'com.android.application'

/*
    $HOME/.gradle/gradle.properties:
    hbn.signing=$HOME/blah/blah/hbn.signing

    $HOME/blah/blah/hbn.signing:
    android {
        signingConfigs {
            debug {
                storeFile file('/path/to/keystore.jks')
                storePassword 'myPassword'
                keyAlias = 'myDebugKey'
                keyPassword 'myPassword'
            }
            beta {
                storeFile file('/path/to/keystore.jks')
                storePassword 'myPassword'
                keyAlias = 'myBetaKey'
                keyPassword 'myPassword'
            }
            release {
                storeFile file('/path/to/keystore.jks')
                storePassword 'myPassword'
                keyAlias = 'myReleaseKey'
                keyPassword 'myPassword'
            }
        }

        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
            beta {
                signingConfig signingConfigs.beta
            }
            release {
                signingConfig signingConfigs.release
            }
        }
    }
 */
if (project.hasProperty("hbn.signing")
        && new File(project.property("hbn.signing")).exists()) {
    apply from: project.property("hbn.signing")
}

/*
    $HOME/.gradle/gradle.properties:
    NeverTooManyBooks.email=$HOME/blah/blah/NeverTooManyBooks.email

    $HOME/blah/blah/NeverTooManyBooks.email:
    android {
        defaultConfig {
            // ACRA crash reports
            buildConfigField('String', 'EMAIL_ACRA', '"acra@somewhere.com"')
            // Manual debug reports
            buildConfigField('String', 'EMAIL_DEBUG_REPORT', '"debug@somewhere.com"')
            // About-page contact
            buildConfigField('String', 'EMAIL_CONTACT', '"info@somewhere.com"')
        }
    }
 */
if (project.hasProperty("NeverTooManyBooks.email")
        && new File(project.property("NeverTooManyBooks.email")).exists()) {
    apply from: project.property("NeverTooManyBooks.email")
}

android {
    compileSdkVersion 30

    // https://gs.statcounter.com/os-version-market-share/android/mobile-tablet/worldwide
    // 2021-06-30 world wide:
    // 11.0     17.5
    // 10.0     53.41
    // 9.0      70.44
    // 8.1      79.5
    // 8.0      83.48

    defaultConfig {
        applicationId 'com.hardbacknutter.nevertoomanybooks'
        versionCode 12
        versionName = "2.5.4"

        // see NOTES.txt
        minSdkVersion 26
        targetSdkVersion 30

        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // Email address placeholders. Their values should be empty, i.e. '""'.
        // The real values are in "NeverTooManyBooks.email"; see above
        if (getBuildConfigFields().get('EMAIL_ACRA') == null) {
            // ACRA crash reports
            buildConfigField('String', 'EMAIL_ACRA', '""')
            // Manual debug reports
            buildConfigField('String', 'EMAIL_DEBUG_REPORT', '""')
            // About-page contact
            buildConfigField('String', 'EMAIL_CONTACT', '""')
        }

        // Add the build UTC DateTime; ISO formatted
        buildConfigField('String', 'TIMESTAMP',
                '"' + LocalDateTime.now(ZoneOffset.UTC)
                        .format(DateTimeFormatter.ISO_LOCAL_DATE_TIME) + '"')


        buildConfigField('boolean', 'ENABLE_GOOGLE_BOOKS', 'true')

        // Dutch.
        // 2021-04: the site has changed technology and our engine no longer works.
        // Disabled for now.
        buildConfigField('boolean', 'ENABLE_KB_NL', 'false')

        // Dutch.
        buildConfigField('boolean', 'ENABLE_LAST_DODO', 'true')
        // Dutch.
        buildConfigField('boolean', 'ENABLE_STRIP_INFO', 'true')

        buildConfigField('boolean', 'ENABLE_LIBRARY_THING_ALT_ED', 'true')

        // under development
        buildConfigField('boolean', 'ENABLE_STYLE_BOOKSHELF_FILTER', 'false')
        buildConfigField('boolean', 'ENABLE_STRIP_INFO_LOGIN', 'false')
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        beta {
            initWith release
            versionNameSuffix 'beta'
        }
        debug {
            versionNameSuffix 'debug'

            // under development
            buildConfigField('boolean', 'ENABLE_STYLE_BOOKSHELF_FILTER', 'true')
            // under development
            buildConfigField('boolean', 'ENABLE_STRIP_INFO_LOGIN', 'true')
            // under development; adapting to the new site format.
            buildConfigField('boolean', 'ENABLE_KB_NL', 'true')
        }
    }

    lintOptions {
        // allow command line builds to complete
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        // https://developer.android.com/studio/releases/#4-0-0-desugar
        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        unitTests.includeAndroidResources = true
        // Can be enabled to make Log.x work. OR, use Logger.x
        unitTests.returnDefaultValues = false
        unitTests.all {
            reports {
                junitXml.enabled = true
                html.enabled = false
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/README.md'
        exclude 'META-INF/CHANGES'
    }
}

// AndroidX release notes:  https://developer.android.com/jetpack/androidx/versions
dependencies {
    // We're using an updated/repacked org.json, version 20210307
    implementation project(path: ':repacked-org-json')
    implementation project(path: ':fastscroller')

    // https://github.com/google/desugar_jdk_libs
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    //
    implementation 'androidx.activity:activity:1.3.0-rc02'
    implementation 'androidx.annotation:annotation:1.3.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.4.0-alpha03'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0-rc01'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.core:core:1.7.0-alpha01'
    implementation 'androidx.customview:customview:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    //implementation 'androidx.work:work-runtime:2.5.0'

    // https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.5.0-alpha01'

    // https://www.acra.ch/docs/Setup/
    implementation 'ch.acra:acra-mail:5.8.4'
    implementation 'ch.acra:acra-toast:5.8.4'
    implementation 'ch.acra:acra-dialog:5.8.4'

    // Tar file support for backups
    // http://commons.apache.org/proper/commons-compress/
    implementation 'org.apache.commons:commons-compress:1.21'
    // http://commons.apache.org/proper/commons-codec/
    implementation 'commons-codec:commons-codec:1.13'

    // jsoup HTML parser library @ https://jsoup.org/
    implementation 'org.jsoup:jsoup:1.14.1'

    // Embedded ZXing: https://github.com/journeyapps/zxing-android-embedded
    implementation 'com.journeyapps:zxing-android-embedded:4.2.0'
    implementation 'com.google.zxing:core:3.4.1'

    // local tests use JUnit5
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.11.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    // device tests use JUnit4
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

//    testImplementation 'org.robolectric:robolectric:4.4'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
